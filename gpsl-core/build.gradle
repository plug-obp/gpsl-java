plugins {
    id 'java'
    id 'antlr'
}

group = 'fr.ensta'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    modularity.inferModulePath = true
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(23))
    }
}

compileJava {
    options.compilerArgs.add('--enable-preview')
}

compileTestJava {
    options.compilerArgs.add('--enable-preview')
}

dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: '4.13.2'
    //api project(':obp3-algos')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    jvmArgs += '--enable-preview'
}

generateGrammarSource {
    outputDirectory = layout.buildDirectory.dir('generated-src/antlr/main/gpsl/parser').get().asFile
    arguments = [
            '-package', 'gpsl.parser',
            '-Xexact-output-dir'
    ]
}

/**
 * the following is needed because [antlr plugin wiring bug](https://github.com/gradle/gradle/issues/25885#issue-1818904631)
 * [workaround](https://github.com/gradle/gradle/issues/19555#issuecomment-1593252653)
 */
sourceSets.configureEach {
    var generateGrammarSource = tasks.named(getTaskName("generate", "GrammarSource"))
    java.srcDir(generateGrammarSource.map { files() })
}
