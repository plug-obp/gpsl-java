plugins {
    id 'java'
    id 'antlr'
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/plug-obp/obp3-core")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

java {
    modularity.inferModulePath = true
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(23))
    }
}

compileJava {
    options.compilerArgs.add('--enable-preview')
}

compileTestJava {
    options.compilerArgs.add('--enable-preview')
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.2'
    implementation ('org.obpcdl:obp3-runtime:1.0.+')
    implementation project(':gpsl-ltl3ba')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    jvmArgs += '--enable-preview'
}

generateGrammarSource {
    outputDirectory = layout.buildDirectory.dir('generated-src/antlr/main/gpsl/parser').get().asFile
    packageName = 'gpsl.parser'
    arguments = [
            '-Xexact-output-dir'
    ]
}

/**
 * the following is needed because [antlr plugin wiring bug](https://github.com/gradle/gradle/issues/25885#issue-1818904631)
 * [workaround](https://github.com/gradle/gradle/issues/19555#issuecomment-1593252653)
 */
sourceSets.configureEach {
    var generateGrammarSource = tasks.named(getTaskName("generate", "GrammarSource"))
    java.srcDir(generateGrammarSource.map { files() })
}
